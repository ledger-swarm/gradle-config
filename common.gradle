
////////////////////////////////////////////////////////////////////////////////
//
// Set group and version
//
////////////////////////////////////////////////////////////////////////////////

// Use GIT to get the version number
group 'io.ledgerswarm'
pluginManager.withPlugin("com.palantir.git-version") {
  if (
      versionDetails().branchName ==~ /^(master)|(release.*)|(patch.*)$/
          && versionDetails().isCleanTag
          && gitVersion() ==~ /^\d+(\.\d+)+(-.+)?$/
  ) {
    project.version = gitVersion()
  } else {
    project.version = '100-SNAPSHOT'
  }
}

////////////////////////////////////////////////////////////////////////////////
//
// Where to load library code from
//
////////////////////////////////////////////////////////////////////////////////
repositories {
  // Maven local only for our own snapshot builds
  mavenLocal {
    content {
      snapshotsOnly()
      includeGroupAndSubgroups('io.setl')
      includeGroupAndSubgroups('io.ledgerswarm')
    }
  }

  // check central repository
  mavenCentral()

  // check ledger swarm specific repository
  maven {
    url = 'https://maven.pkg.github.com/ledger-swarm/maven-repo'
    credentials {
      username = "$githubUser"
      password = "$githubToken"
    }
  }
}


pluginManager.withPlugin("idea") {
  idea {
    module {
      downloadJavadoc = true
      downloadSources = true
    }
  }
}


////////////////////////////////////////////////////////////////////////////////
//
// Configure testing
//
////////////////////////////////////////////////////////////////////////////////
test {
  useJUnitPlatform()
  environment("RUN_DOCKER_TESTS", "true")
}

////////////////////////////////////////////////////////////////////////////////
//
// Perform code quality analysis with checkstyle
//
////////////////////////////////////////////////////////////////////////////////
pluginManager.withPlugin("checkstyle") {
  checkstyle {
    toolVersion = "10.13.0"
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
  }

  // Do not perform checkstyle checks on test code
  checkstyleTest {
    enabled = false
  }

  // Guava conflict with checkstyle plugin.
  configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
      select("com.google.guava:guava:0")
    }
  }
}
////////////////////////////////////////////////////////////////////////////////
//
// Perform code quality analysis with spotbugs
//
////////////////////////////////////////////////////////////////////////////////
pluginManager.withPlugin("com.github.spotbugs") {
  spotbugs {
    excludeFilter = rootProject.file('config/spotbugs/spotbugs-exclude.xml')
  }

  spotbugsMain {
    reports {
      html {
        required = true
      }
      xml {
        required = false
      }
    }
  }

// Do not perform spotbugs analysis on test code
  spotbugsTest {
    enabled = false
  }
}

////////////////////////////////////////////////////////////////////////////////
//
// Ensure the Javadoc and sources are packaged
//
////////////////////////////////////////////////////////////////////////////////
java {
  withJavadocJar()
  withSourcesJar()
}

javadoc {
  // Exclude generated code and test code from javadoc
  source = sourceSets.main.allJava.filter { !(it.path.contains('/generated/') || it.path.contains('/src/test/')) }
  options.addStringOption('Xdoclint:none', '-quiet')
  failOnError = false  
}

////////////////////////////////////////////////////////////////////////////////
//
// Create a build properties file in the output jar
//
////////////////////////////////////////////////////////////////////////////////
tasks.register('writeBuildProperties', WriteProperties) {
  destinationFile = file("$buildDir/ls-build.properties")
  property("project.name", project.name)
  property("project.group", project.group)
  property("project.version", project.version)
  property("buildTime", java.time.Instant.now())

  pluginManager.withPlugin("com.palantir.git-version") {
    def gitDetails = versionDetails()
    property("git.lastTag", gitDetails.lastTag)
    property("git.commitDistance", gitDetails.commitDistance)
    property("git.gitHash", gitDetails.gitHash)
    property("git.gitHashFull", gitDetails.gitHashFull)
    property("git.branchName", gitDetails.branchName)
    property("git.isCleanTag", gitDetails.isCleanTag)
  }
}

jar {
  dependsOn writeBuildProperties
  from("$buildDir/ls-build.properties") {
    include "ls-build.properties"
    into "META-INF"
  }
}


////////////////////////////////////////////////////////////////////////////////
//
// Publishing
//
////////////////////////////////////////////////////////////////////////////////
pluginManager.withPlugin('maven-publish') {
  // Configuration for publishing
  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = project.name

        from components.java
      }
    }
    repositories {
      maven {
        name = 'github'
        url = 'https://maven.pkg.github.com/ledger-swarm/maven-repo'
        credentials {
          username = "$githubUser"
          password = "$githubToken"
        }
      }
    }
  }
}



////////////////////////////////////////////////////////////////////////////////
//
// Swagger
//
////////////////////////////////////////////////////////////////////////////////
pluginManager.withPlugin('io.swagger.core.v3.swagger-gradle-plugin') {
  resolve {
    outputFileName = project.name
    outputFormat = 'JSON'
    prettyPrint = 'TRUE'
    classpath = sourceSets.main.runtimeClasspath
    resourcePackages = [
        'io.ledgerswarm'
    ]
    outputDir = new File("$project.buildDir/swagger")
  }
}

pluginManager.withPlugin('io.setl.gradle.swagger') {
  swaggerFix {
    apiFile = "$project.buildDir/swagger/${project.name}.json"
  }
}


////////////////////////////////////////////////////////////////////////////////
//
// Load overrides
//
////////////////////////////////////////////////////////////////////////////////
def loadOverrides(path,quiet) {
  File f = file(path)
  if (f.exists() && !f.isDirectory()) {
    println "Loading overrides from \""+f.absolutePath+"\"";
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(f))
    localProperties.each {
      prop ->
        {
          println "Override: " + prop.key + " = " + prop.value
          project.ext.set(prop.key, prop.value)
        }
    }
  } else if(! quiet ) {
    println "WARNING: Override file set to \"" + f.absolutePath + "\" but file does not exist.";
  }

}

// Project file
loadOverrides("$project.rootDir/override.properties", true);

// Project property. This can be set on command-line or via the "ORG_GRADLE_PROJECT_overrideFile" environment variable.
if (hasProperty("overrideFile")) {
  loadOverrides(property("overrideFile"), false)
}

// Environment variable.
if( System.getenv().containsKey("OVERRIDE_FILE") ) {
  loadOverrides(System.getenv().get("OVERRIDE_FILE"), false);
}



////////////////////////////////////////////////////////////////////////////////
//
// Ensure "-parameters" option is present on compile
//
////////////////////////////////////////////////////////////////////////////////
compileJava {
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.compilerArgs << '-parameters'
}
